// This is the grammar for "lang"

// A program is a list of statements
<program> ::= <statement_list>
<statement_list> ::= <statement_list> <statement> | epsilon

// A statement is either: assignment, expression, or return
// semicolons are only needed for statements
<statement> ::= <assignment> ";" | <expression> ";" | <return> ";"

// Return
<return> ::= "return" <expression>

// Assignment
<assignment> ::=  <value_assignment> | <function_assignment>
<value_assignment> ::= <type> <identifier> "=" <expression>
<function_assignment> ::= "def" <identifier> "=" <function>
<type> ::= "i32" | "bool" | "string" 
<identifier> ::= [a-zA-Z_][a-zA-Z0-9_]*

// Expression 
<expression> ::= <call> | <literal> | <identifier> | <function> | <binary_expr> | <if_expr> | "(" <expression> ")"
<binary_expr> ::= <expression> <operator> <expression>
<if_expr> ::= "if" "(" <expression> ")" <expression> ("else" <expression>)?

// Operator
<operator> ::= "+" | "-" | "*" | "/" | "==" | "!=" | "<" | "<=" | ">" | ">=" | "&&" | "||" | "%"

// Literal
<literal> ::= <integer_literal> | <boolean_literal> | <string_literal>
<integer_literal> ::= [0-9]+
<boolean_literal> ::= "true" | "false"
<string_literal>  ::= '"' [^"]* '"'

// Function (anonymous)
<function> ::= "fn" "(" <parameter_list> ")" "->" <type> "{"  <statement_list> "}"
<parameter_list> ::= <parameter> ("," <parameter>)*
<parameter> ::= <identifier> ":" <type>

// Call
<call> ::= <expression> "(" <argument_list> ")"
<argument_list> ::= <expression> ("," <expression>)*
